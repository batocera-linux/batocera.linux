ODROIDN2-UBOOT-CONFIG

setenv board "odroidn2"
setenv display_autodetect "true"
setenv hdmimode "1080p60hz"
setenv monitor_onoff "false"
setenv overscan "100"
setenv sdrmode "auto"
setenv voutmode "hdmi"
setenv disablehpd "false"
setenv cec "true"
setenv disable_vu7 "true"

if test "${variant}" = "n2_plus"; then
	setenv max_freq_a73 "2208"
	setenv max_freq_a53 "1908"
else
        setenv max_freq_a73 "1800"
        setenv max_freq_a53 "1896"
fi

setenv maxcpus "6"

# Set load addresses
setenv dtb_loadaddr "0x1000000"
setenv boot_loadaddr "0x01080000"
setenv initrd_loadaddr "0x3700000"
setenv loadaddr "0x1080000"

load mmc ${devno}:1 ${loadaddr} config.ini \
    && ini generic ${loadaddr}
if test "x${overlay_profile}" != "x"; then
    ini overlay_${overlay_profile} ${loadaddr}
fi

# Default Console Device Setting
setenv condev "console=ttyS0,115200n8"   # on both

### Normal HDMI Monitors
if test "${display_autodetect}" = "true"; then hdmitx edid; fi
if test "${hdmimode}" = "custombuilt"; then setenv cmode "modeline=${modeline}"; fi
if test "${cec}" = "true"; then setenv cec_enable "hdmitx=cec3f"; fi
if test "${disable_vu7}" = "false"; then setenv hid_quirks "usbhid.quirks=0x0eef:0x0005:0x0004"; fi

# Boot Args
setenv bootargs "label=BATOCERA console=tty3 quiet loglevel=0 consoleblank=0 vt.global_cursor_default=0 hdmimode=${hdmimode} cvbsmode=576cvbs max_freq_a53=${max_freq_a53} max_freq_a73=${max_freq_a73} maxcpus=${maxcpus} voutmode=${voutmode} ${cmode} disablehpd=${disablehpd} cvbscable=${cvbscable} overscan=${overscan} ${hid_quirks} monitor_onoff=${monitor_onoff} logo=osd0,loaded ${cec_enable} sdrmode=${sdrmode} n2.variant=${variant}"

# Load kernel, dtb and initrd
fatload mmc ${devno}:1 ${dtb_loadaddr} boot/meson-g12b-odroid-n2.dtb
fatload mmc ${devno}:1 ${boot_loadaddr} boot/linux
fatload mmc ${devno}:1 ${initrd_loadaddr} boot/uInitrd
fdt addr ${dtb_loadaddr}

if test "x${overlay_profile}" != "x"; then
    ini overlay_${overlay_profile} ${config_addr_r}
fi

if test "x{overlays}" != "x"; then
    fdt resize ${overlay_resize}
    for overlay in ${overlays}; do
        load mmc ${devno}:1 ${dtbo_addr_r} amlogic/overlays/${board}/${overlay}.dtbo \
            && fdt apply ${dtbo_addr_r}
    done
fi

# boot
bootm ${loadaddr} ${initrd_loadaddr} ${dtb_loadaddr}
